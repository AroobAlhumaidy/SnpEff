#!/usr/bin/env bds

#-------------------------------------------------------------------------------
#
# Download genome data
#
#-------------------------------------------------------------------------------

include 'downloadEnsembl'
include 'downloadHumanDb'
include 'downloadNcbi'
include 'downloadOther'
include 'downloadUcsc'

#-------------------------------------------------------------------------------
# Fetch (wget) a file to current dir
# Returns true if download command is executed
#-------------------------------------------------------------------------------
bool wget(string url) {
	return wgetLocalDir(url, ".", true)
}

bool wgetTask(string url) {
	return wgetLocalDir(url, ".", false)
}

#-------------------------------------------------------------------------------
# Fetch (wget) a file, provide local dir path
# Returns true if download command is executed (or task created)
#-------------------------------------------------------------------------------
bool wgetLocalDir(string url, string localDir, bool doWait) {
	base := url.baseName()
	localFile := "$localDir/$base"
	return wgetLocalFile(url, localFile, doWait)
}

#-------------------------------------------------------------------------------
# Fetch (wget) a file, provide local file path
# Returns true if download command is executed (or task created)
#-------------------------------------------------------------------------------
bool wgetLocalFile(string url, string localFile, bool doWait) {
	if( localFile.isEmpty() ) {
		localFile = url.baseName()
	}

	if( localFile.exists() ) {
		print "Local file '$localFile' exists, not downloading"
		return false
	}

	print "Downloading '$url' to '$localFile'"
	if( doWait ) {
		sys $wgetCmd '$url'
	} else {
		task $wgetCmd '$url'
	}
	return true
}
