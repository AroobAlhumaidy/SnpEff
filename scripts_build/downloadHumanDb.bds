#!/usr/bin/env bds

#-------------------------------------------------------------------------------
#
# Download genome data
#
#-------------------------------------------------------------------------------

clinvar            := "clinvar.vcf.gz"
urlClinVarGrch37   := "ftp://ftp.ncbi.nlm.nih.gov/pub/clinvar/vcf_GRCh37/$clinvar"
urlClinVarGrch38   := "ftp://ftp.ncbi.nlm.nih.gov/pub/clinvar/vcf_GRCh38/$clinvar"
urlDbNsfp          := "ftp://dbnsfp:dbnsfp@dbnsfp.softgenetics.com/dbNSFPv$dbNsfpVer\.zip"
urlDbSnpGrch37     := "ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606_b151_GRCh37p13/VCF/00-All.vcf.gz"
urlDbSnpGrch38     := "ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606/VCF/00-All.vcf.gz"
urlGwasCatalog     := "http://www.genome.gov/admin/gwascatalog.txt"
urlJaspar          := "http://jaspar.binf.ku.dk/html/DOWNLOAD/JASPAR_CORE/pfm/redundant/pfm_all.txt"
urlNextProt        := "ftp://ftp.nextprot.org/pub/current_release/xml"
urlPdb             := "rsync.wwpdb.org::ftp_data/structures/divided/pdb/"

#-------------------------------------------------------------------------------
# Build dbNSFP database for genome 'hg'
#-------------------------------------------------------------------------------
void buildDbNsfp(string genome) {
	dirDbNsfp := "$dirDb/$genome/dbNSFP"
	base := "$dirDbNsfp/dbNSFP$dbNsfpVer\_variant.chr"
	dbNsfp := "$dirDbNsfp/dbNSFP$dbNsfpVer\.txt.gz"

	string[] txts
	header := ""
	for(string file : dirDb.dir()) {
		if(!file.startsWith("dbNSFP$dbNsfpVer\_variant") || file.endsWith("gz")) continue
		file = dirDb + '/' + file.trim()

		# Create header file
		if( header.isEmpty() ) {
			header = "$dirDb/dbNSFP$dbNsfpVer\.header"
			println("Creating headr file 'header'")
			sys head -n 1 $file > $header
		}

		# Sort files
		println("File: $file")
		base := file.baseName()
		sorted := "$dirDb/$base\.sort.txt"
		txts += sorted

		task( sorted <- file , cpus := 2) {
			sys cat $file \
				| $dirScriptsBuild/dbNSFP_sort.pl \
				| grep -v "^#" \
				> $sorted
		}
	}

	# Compress and create index
	txtsStr := txts.join(' ')
	task( dbNsfp <- txts ) {
		sys cat $header $txtsStr | bgzip -c > $dbNsfp
		sys tabix -s 1 -b 2 -e 2 $dbNsfp
	}
}

#-------------------------------------------------------------------------------
# Download ClinVar
#-------------------------------------------------------------------------------
void downloadClinvar() {
	println "Download Clinvar"

	# GRCh37
	dir := "$dirDb/GRCh37/clinvar/"
	dir.mkdir()
	dir.chdir()
	if( !clinvar.exists() ) fetch(urlClinVarGrch37)

	# GRCh38
	dir = "$dirDb/GRCh38/clinvar/"
	dir.mkdir()
	dir.chdir()
	if( !clinvar.exists() ) fetch(urlClinVarGrch38)

	wait
}

#-------------------------------------------------------------------------------
# Download dbNSFP
#-------------------------------------------------------------------------------
void downloadDbNsfp() {
	println "Download dbNSFP"

	hg := 'GRCh38'
	dir := "$dirDb/$hg/dbNSFP/"
	dir.mkdir()
	dir.chdir()

	base := "dbNSFPv$dbNsfpVer"
	baseChr := "dbNSFP$dbNsfpVer"
	zip := "$base\.zip"

	# Download
	if( !zip.exists() ) {
		println "Download dbNsfp $urlDbNsfp ($zip)"
		fetch(urlDbNsfp)
	}

#	# Unzip
#	chr1 := "$baseChr\_variant.chr1"
#	if( !chr1.exists() ) {
#		print "Uncompressing dbNsfp: $zip ($chr1)\n"
#		sys 7za x $zip
#	}

	buildDbNsfp(hg)
	wait
}

#-------------------------------------------------------------------------------
# Download dbSnp
#-------------------------------------------------------------------------------
void downloadDbSnp() {
	println "Download dbSnp"

	dbsnp := "dbsnp.vcf.gz"

	# GRCh37
	dir := "$dirDb/GRCh37/dbSnp/"
	dir.mkdir()
	dir.chdir()
	if( !dbsnp.exists() ) {
		if( fetch(urlDbSnpGrch37) ) {
			sys cp 00-All.vcf.gz $dbsnp
		}
	}

	# GRCh38
	dir = "$dirDb/GRCh38/dbSnp/"
	dir.mkdir()
	dir.chdir()
	if( !dbsnp.exists() ) {
		if( fetch(urlDbSnpGrch38) ) {
			sys cp 00-All.vcf.gz $dbsnp
		}
	}

	wait
}

#-------------------------------------------------------------------------------
# Download GWAS catalog
#-------------------------------------------------------------------------------
void downloadGwasCatalog() {
	println "Download Gwas-Catalog"

	dir := "$dirDb/gwasCatalog"
	dir.mkdir()
	dir.chdir()

	if( fetch(urlGwasCatalog) ) {
		sys cat gwascatalog.txt | $dirScriptsBuild/gwascatalog2vcf.pl > gwascatalog.vcf
	}

	wait
}

#-------------------------------------------------------------------------------
# Download Jaspar
#-------------------------------------------------------------------------------
void downloadJaspar() {
	println "Download Jaspar"

	dir := "$dirDb/jaspar"
	dir.mkdir()
	dir.chdir()

	if( fetch(urlJaspar) ) {
		sys gzip -c pfm_all.txt > pwms.bin
		println "Jaspar: File $dir/pwms.bin created"
	}
}

#-------------------------------------------------------------------------------
# Download NextProt
#-------------------------------------------------------------------------------
void downloadNextProt() {
	println "Download NextProt"

	dir := "$dirDb/nextProt"
	dir.mkdir()
	dir.chdir()
	
	downloadOk := "download.ok"
	if( ! downloadOk.exists() ) {
		println "Download NextProt"

		# Download each chromosome's XML file
		for( string chr : humanChrsDd ) {
			fetch("$urlNextProt/nextprot_chromosome_$chr\.xml.gz")
		}

		downloadOk.write("OK")	# Mark as downlaoded OK
	}
}

#-------------------------------------------------------------------------------
# Download PDB
#-------------------------------------------------------------------------------
void downloadPdb() {
	println "Download PDB"

	dir := "$dirDb/pdb"
	dir.mkdir()
	dir.chdir()

	downloadOk := "download.ok"
	if( ! downloadOk.exists() ) {
		println "Download PDB"
		sys rsync -rlpt -v -z --delete --port=33444 $urlPdb .
		downloadOk.write("OK")	# Mark as downlaoded OK
	}
}

