#!/usr/bin/env bds

# Build files from a set of NCBI downloaded files
#
# This script assumes that:
#
#	1) Current path is the genome directory
#
#	2) Original (downloaded) files are:
#			ORI/cds.fa.gz     : CDS/RNA sequences
#			ORI/genes.gff.gz  : Gene models in GFF3 format
#			ORI/protein.fa.gz : Proteins FASTA file
#			ORI/sequence.fa.gz: Chromosome sequences
#      If names differ, you need to rename the files accordingly
# 

# Path to scripts
snpeffDir  := "$HOME/snpEff"
dbDir      := "."
scriptsDir := "$dbDir"
oriDir     := "$dbDir/ORI"

# File names
cdsOri     := "$oriDir/cds.fa.gz"
cds        := "$dbDir/cds.fa.gz"
gffGzOri   := "$oriDir/genes.gff.gz"
gff        := "$dbDir/genes.gff.gz"
proteinOri := "$oriDir/protein.fa.gz"
protein    := "$dbDir/protein.fa.gz"
seqOri     := "$oriDir/sequences.fa.gz"
seq        := "$dbDir/sequences.fa.gz"

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

println "Processing GFF file"

# Create reference FASTA files
println "Processing reference FASTA files"
task( seq <- seqOri ) {
	sys gunzip -c $seqOri \
			| sed "s/^>gi|.*|ref|\(.._.*\)|.*/>\1/" \
			| gzip -c \
			> $seq
}

# Uncompress gff
gffOri := gffGzOri.removeExt('.gz')
task(gffOri <- gffGzOri) {
	sys gunzip -c $gffGzOri > $gffOri
}

# Fix GFF ids
task(gff <- gffOri) {
	sys $scriptsDir/fix_gff.pl $gffOri \
			| gzip -c \
			> $gff
}

println "Processing protein FASTA files"
task(protein <- [proteinOri,gff]) {
	sys gunzip -c $proteinOri \
			| sed "s/^>gi|[0-9]*|ref|\(.*\)|.*/>\1/" \
			| $scriptsDir/fix_fasta_protein_cds.pl protein_id.map.txt \
			| gzip -c \
			> $protein
}

println "Processing RNA FASTA files"
task( cds <- [cdsOri,gff]) {
	# Do we need this 'fix_fasta_protein_cds.pl' step?
	#	| $scriptsDir/fix_fasta_protein_cds.pl ids.map.txt \

	sys gunzip -c $cdsOri \
			| sed "s/^>gi|[0-9]*|ref|\(.*\)|.*/>\1/" \
			| gzip -c \
			> $cds
}

wait
println "Done."
